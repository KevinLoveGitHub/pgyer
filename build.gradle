plugins {
    id 'groovy'
    id 'java'
    id 'maven'
    id 'maven-publish'
    id "java-gradle-plugin"
    id "com.jfrog.bintray" version "1.8.4"
}

group 'com.kangxiaoguang.gradle.tools'
version '1.1'

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    google()
}

uploadArchives {
    repositories.mavenDeployer {
        repository(url: uri('./repo')) //仓库的路径，此处是项目根目录下的 repo 的文件夹
        pom.artifactId = 'pgyer' //artifactId ，自行定义
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            url 'https://github.com/KevinLoveGitHub/pgyer'

            packaging 'aar'
            //artifactId 'auto-app-version' //如果不设置默认使用settings.gradle中的配置 rootProject.name = 'auto-app-version'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/KevinLoveGitHub/pgyer.git'
                url 'https://github.com/KevinLoveGitHub/pgyer'
            }
            developers {
                developer {
                    name 'Kevin.Kang'
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'pgyer'
        desc = 'Upload apk to pgyer' //库文件描述
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/KevinLoveGitHub/pgyer.git'
        websiteUrl = 'https://github.com/KevinLoveGitHub/pgyer'
        issueTrackerUrl = 'https://github.com/KevinLoveGitHub/pgyer/issues'
        publicDownloadNumbers = true
        githubRepo = 'KevinLoveGitHub/pgyer' //Optional Github repository
        githubReleaseNotesFile = 'README.md' //Optional Github readme file
        publish = true
        version {
            //name = '0.1' //如果不设置默认使用build.gradle中的version变量
            desc = 'Upload apk to pgyer' //版本描述
            released  = new Date()
            attributes = ['gradle-plugin': 'com.kangxiaoguang.gradle.tools:pgyer']
        }
    }
}


dependencies {
    compile gradleApi()
    compile localGroovy()

    compile 'org.json:json:20190722'

    // https://mvnrepository.com/artifact/com.squareup.okhttp3/okhttp
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.2.2'

    runtime group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.2.2'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'

    // https://mvnrepository.com/artifact/com.squareup.okhttp3/okhttp-urlconnection
    compile group: 'com.squareup.okhttp3', name: 'okhttp-urlconnection', version: '4.2.2'

    compile 'com.android.tools.build:gradle:2.3.3'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}
